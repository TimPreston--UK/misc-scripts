#!/usr/bin/perl

use strict;
use Config::Auto;
use DateTime::Format::DateParse;
use WebService::NotifyMyAndroid;
use WWW::Mechanize;

my $conf = Config::Auto::parse;

my @required_conf_options = qw(nma_key house_num postcode);
if (!$conf || grep { !exists $conf->{$_} } @required_conf_options) {
    die "Must provide a conf file (see Config::Auto) with options: "
        . join ', ', @required_conf_options;
}

my $mech = WWW::Mechanize->new;

$mech->get('http://www.stevenage.gov.uk/find/');

$mech->submit_form(
    with_fields => {
        # What the fuck kind of field names are these?  Fucking ASP.net
        # bollocks.
        'ctl00$ctl00$ContentPlaceHolder1$ContentPlaceHolder1$txtNumber' => 5,
        'ctl00$ctl00$ContentPlaceHolder1$ContentPlaceHolder1$txtPostcode' => 'SG2 7DP',
    },
    button => 'ctl00$ctl00$ContentPlaceHolder1$ContentPlaceHolder1$BtnLookup',
);

if (my ($date, $type) = $mech->content =~ 
    m{Your next rubbish collection is on <strong>(.+) \((.+)\)</strong>}m)
{
    # Work out if that's tomorrow.
    my $dt = DateTime::Format::DateParse->parse_datetime($date);
    if ($dt->dmy eq DateTime->now->add( days => 1)->dmy) {
        my $api_keys = ref $conf->{nma_key} eq 'ARRAY'
            ? $conf->{nma_key}
            : [ $conf->{nma_key} ];
        my $nma = WebService::NotifyMyAndroid->new;
        my $message = $nma->notify(
            apikey => $api_keys,
            application => 'Rubbish collection',
            event       => "$type collection tomorrow",
            description => "$type collection tomorrow morning ($date)",
        );
        defined( $message->{success} ) or die( $message->{error}->{content} );
    }
} else {
    die "Failed to identify next collection in response";
}
